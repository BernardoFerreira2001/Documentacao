/********************  BLYNK CONFIG  ********************/
#define BLYNK_TEMPLATE_ID "TMPL5u-RkcADe"
#define BLYNK_TEMPLATE_NAME "GrowLb"
#define BLYNK_AUTH_TOKEN "QVjZo0V_oGcaHCQv21qOk4PAcCYK5DM7"

#define BLYNK_PRINT Serial

#include <WiFi.h>
#include <WiFiClient.h>
#include <HTTPClient.h>
#include <BlynkSimpleEsp32.h>
#include "DHT.h"

/********************  OLED INCLUDES  ********************/
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>

/********************  API CONFIG  ********************/
String apiUrl = "http://192.168.1.116:8000";
int deviceId = 1;

/********************  DHT22  ********************/
#define DHTPIN 4
#define DHTTYPE DHT22
DHT dht(DHTPIN, DHTTYPE);

/********************  PINOS  ********************/
#define LED_PIN 21
#define RELAY_FAN_PIN 5
#define RELAY_MIST_PIN 23
#define RELAY_PUMP_PIN 22
#define SOIL_PIN 34

/********************  BLYNK VPINS   ********************/
#define VPIN_TEMP        V0
#define VPIN_HUM_AIR     V1
#define VPIN_HUM_SOIL    V6

/********************  OLED CONFIG  ********************/
#define SCREEN_WIDTH 128
#define SCREEN_HEIGHT 64
#define OLED_RESET -1
#define OLED_SDA 19
#define OLED_SCL 18

Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, OLED_RESET);

/********************  CALIBRAÇÃO SOLO  ********************/
#define SOIL_DRY 3600
#define SOIL_WET 1500

#define RELAY_ON  LOW
#define RELAY_OFF HIGH

/********************  ESTADOS  ********************/
bool ledManualMode  = false;
bool fanManualMode  = false;
bool mistManualMode = false;
bool pumpManualMode = false;

bool ledState  = false;
bool fanState  = false;
bool mistState = false;
bool pumpState = false;

/********************  TIMERS  ********************/
unsigned long lastPrint = 0;
unsigned long lastLedToggle = 0;
unsigned long lastApiSend = 0;
unsigned long lastBlynkSend = 0;

/********************  WIFI  ********************/
char ssid[] = "Vodafone-17CAAF";
char pass[] = "CyufGW6mzK";

/********************  API – SENSOR ********************/
void enviarSensorParaAPI(float temp, float humAir, int humSoil) {
  HTTPClient http;
  String endpoint = apiUrl + "/devices/" + String(deviceId) + "/sensor";

  http.begin(endpoint);
  http.addHeader("Content-Type", "application/json");

  String payload =
    "{"
      "\"temperature\":" + String(temp) + "," +
      "\"humidity_air\":" + String(humAir) + "," +
      "\"humidity_soil\":" + String(humSoil) +
    "}";

  int code = http.POST(payload);
  Serial.printf("[API SENSOR] %d\n", code);

  http.end();
}

/********************  API – ATUADORES ********************/
void enviarAtuadoresParaAPI() {
  HTTPClient http;
  String endpoint = apiUrl + "/devices/" + String(deviceId) + "/actuators";

  http.begin(endpoint);
  http.addHeader("Content-Type", "application/json");

  String payload =
    "{"
      "\"fan_state\":" + String(fanState) + "," +
      "\"mist_state\":" + String(mistState) + "," +
      "\"led_state\":" + String(ledState) + "," +
      "\"pump_state\":" + String(pumpState) + "," +
      "\"mode\":\"auto\"," +
      "\"source\":\"esp32\"" +
    "}";

  int code = http.POST(payload);
  Serial.printf("[API ACTUATORS] %d\n", code);

  http.end();
}

/********************  BLYNK – ESTADOS ********************/
void atualizarEstadosBlynk() {
  Blynk.virtualWrite(V10, fanState);
  Blynk.virtualWrite(V11, ledState);
  Blynk.virtualWrite(V12, pumpState);
  Blynk.virtualWrite(V13, mistState);
}

/********************  BLYNK CALLBACKS  ********************/
BLYNK_WRITE(V2) {
  ledManualMode = param.asInt();
  ledState = ledManualMode;
  digitalWrite(LED_PIN, ledState ? HIGH : LOW);
  enviarAtuadoresParaAPI();
}

BLYNK_WRITE(V3) {
  fanManualMode = param.asInt();
  fanState = fanManualMode;
  digitalWrite(RELAY_FAN_PIN, fanState ? RELAY_ON : RELAY_OFF);
  enviarAtuadoresParaAPI();
}

BLYNK_WRITE(V4) {
  mistManualMode = param.asInt();
  mistState = mistManualMode;
  digitalWrite(RELAY_MIST_PIN, mistState ? RELAY_ON : RELAY_OFF);
  enviarAtuadoresParaAPI();
}

BLYNK_WRITE(V5) {
  pumpManualMode = param.asInt();
  pumpState = pumpManualMode;
  digitalWrite(RELAY_PUMP_PIN, pumpState ? RELAY_ON : RELAY_OFF);
  enviarAtuadoresParaAPI();
}

/********************  SETUP  ********************/
void setup() {
  Serial.begin(115200);
  dht.begin();

  pinMode(LED_PIN, OUTPUT);
  pinMode(RELAY_FAN_PIN, OUTPUT);
  pinMode(RELAY_MIST_PIN, OUTPUT);
  pinMode(RELAY_PUMP_PIN, OUTPUT);

  digitalWrite(LED_PIN, LOW);
  digitalWrite(RELAY_FAN_PIN, RELAY_OFF);
  digitalWrite(RELAY_MIST_PIN, RELAY_OFF);
  digitalWrite(RELAY_PUMP_PIN, RELAY_OFF);

  WiFi.begin(ssid, pass);
  while (WiFi.status() != WL_CONNECTED) delay(300);

  Blynk.begin(BLYNK_AUTH_TOKEN, ssid, pass);

  Blynk.syncVirtual(V2, V3, V4, V5);

  Wire.begin(OLED_SDA, OLED_SCL);
  display.begin(SSD1306_SWITCHCAPVCC, 0x3C);
  display.clearDisplay();
  display.setTextColor(SSD1306_WHITE);
}

/********************  LOOP  ********************/
void loop() {
  Blynk.run();

  float t = dht.readTemperature();
  float h = dht.readHumidity();
  int soilRaw = analogRead(SOIL_PIN);

  if (isnan(t) || isnan(h)) return;

  int soilPercent = map(soilRaw, SOIL_DRY, SOIL_WET, 0, 100);
  soilPercent = constrain(soilPercent, 0, 100);

  unsigned long now = millis();

  /******** AUTO FAN ********/
  if (!fanManualMode) {
    fanState = t > 26;
    digitalWrite(RELAY_FAN_PIN, fanState ? RELAY_ON : RELAY_OFF);
  }

  /******** AUTO LED ********/
  if (!ledManualMode && now - lastLedToggle > 60000) {
    lastLedToggle = now;
    ledState = !ledState;
    digitalWrite(LED_PIN, ledState ? HIGH : LOW);
  }

  /******** AUTO BOMBA ********/
  if (!pumpManualMode) {
    if (soilPercent < 40) pumpState = true;
    if (soilPercent >= 55) pumpState = false;
    digitalWrite(RELAY_PUMP_PIN, pumpState ? RELAY_ON : RELAY_OFF);
  }

  /******** BLYNK: enviar sensores + estados (a cada 2s) ********/
  if (now - lastBlynkSend > 2000) {
    lastBlynkSend = now;

    Blynk.virtualWrite(VPIN_TEMP, t);
    Blynk.virtualWrite(VPIN_HUM_AIR, h);
    Blynk.virtualWrite(VPIN_HUM_SOIL, soilPercent);

    atualizarEstadosBlynk();
  }

  /******** API: enviar sensores (a cada 30s) ********/
  if (now - lastApiSend > 30000) {
    lastApiSend = now;
    enviarSensorParaAPI(t, h, soilPercent);
  }

  /******** OLED ********/
  display.clearDisplay();
  display.setCursor(0, 0);
  display.printf("T: %.1fC\nH: %.1f%%\nS: %d%%", t, h, soilPercent);
  display.display();
}
